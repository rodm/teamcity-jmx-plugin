
plugins {
    id 'org.gradle.java'
    id 'org.gradle.jacoco'
    id 'com.github.rodm.teamcity-server' version '1.2'
    id 'com.github.rodm.teamcity-environments' version '1.2'
    id 'org.sonarqube' version '2.7'
}

group = 'com.github.rodm'
version = '1.1'

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '10.0'

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "jmx-support"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "${archivesBaseName}-${project.version}.zip"

        descriptor {
            name = 'jmx-support'
            displayName = 'JMX support'
            version = project.version
            description = 'Exposes Server, Agent and Project attributes and related build statistics via JMX.'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
        }

        publish {
            username = findProperty('jetbrains.username')
            password = findProperty('jetbrains.password')
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
        }

        'teamcity2018.1' {
            version = '2018.1'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
        }
    }
}
