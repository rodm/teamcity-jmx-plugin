
plugins {
    id 'org.gradle.java'
    id 'org.gradle.jacoco'
    id 'io.github.rodm.teamcity-server' version '1.5'
    id 'io.github.rodm.teamcity-environments' version '1.5'
    id 'org.sonarqube' version '3.4.0.2513'
}

group = 'com.github.rodm'
version = '1.3-SNAPSHOT'

ext {
    teamcityVersion = project.findProperty('teamcity.api.version') ?: "2018.1"

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

archivesBaseName = "jmx-support"

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "${archivesBaseName}-${project.version}.zip"

        descriptor {
            name = 'jmx-support'
            displayName = 'JMX support'
            version = project.version
            description = 'Exposes Server, Agent and Project attributes and related build statistics via JMX.'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = true
            minimumBuild = '58245'
        }

        publish {
            token = findProperty('jetbrains.token')
            notes = 'Fixes incompatibility with TeamCity 2020.1'
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        'teamcity2018.1' {
            version = '2018.1.5'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
        }

        'teamcity2020.2' {
            version = '2020.2.1'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
        }
    }
}
