
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.8.4'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'maven-publish'
apply plugin: 'sonar-runner'

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

ext {
    publicationVersion = System.properties['version'] ?: version

    buildVcsNumber = System.properties['build.vcs.number'] ?: new Date().format('yyyyMMddHHmmss')
    buildNumber = System.properties['build.number'] ?: 'dev'

    teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'
    teamcityDir = project.hasProperty('teamcity.dir') ? property('teamcity.dir') : "$rootDir/servers"

    repositoryUrl = project.hasProperty('repositoryUrl') ? property('repositoryUrl') : 'http://localhost:8080'
    repositoryUsername = project.hasProperty('repositoryUsername') ? property('repositoryUsername') : ''
    repositoryPassword = project.hasProperty('repositoryPassword') ? property('repositoryPassword') : ''
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

archivesBaseName = "jmx-plugin"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

serverPlugin {
    baseName = project.archivesBaseName
}

teamcity {
    version = "${teamcityVersion}"

    server {
        descriptor = file("$rootDir/src/main/descriptor/teamcity-plugin.xml")
        tokens VERSION: project.version, VCS_REVISION: buildVcsNumber, BUILD_NUMBER: buildNumber

        baseHomeDir = teamcityDir

        environments {
            teamcity81 {
                version = '8.1.5'
                homeDir = file("${baseHomeDir}/TeamCity-${version}")
            }

            teamcity91 {
                version = '9.1.6'
                homeDir = file("${baseHomeDir}/TeamCity-${version}")
            }
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            version publicationVersion

            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
