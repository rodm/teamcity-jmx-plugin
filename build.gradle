
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'sonar-runner'

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

project.ext.teamCityVersion = System.properties['teamcity.version'] ?: '8.1.5'

ext.repositoryUrl = hasProperty('repositoryUrl') ? property('repositoryUrl') : 'http://localhost:8080'
ext.repositoryUsername = hasProperty('repositoryUsername') ? property('repositoryUsername') : ''
ext.repositoryPassword = hasProperty('repositoryPassword') ? property('repositoryPassword') : ''

sourceCompatibility = 1.7
targetCompatibility = 1.7

archivesBaseName = "jmx-plugin"

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
}

dependencies {
    compile group: 'org.jetbrains.teamcity', name: 'server-api', version: "${teamCityVersion}"

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

jar {
    exclude "**/teamcity-plugin.xml"
}

task packagePlugin(type: Zip, dependsOn: jar) {
    destinationDir = file('dist')
    from(jar.outputs.files) {
        into 'server'
    }
    from("$buildDir/resources/main") {
        include 'teamcity-plugin.xml'
    }
}

assemble.dependsOn packagePlugin

artifacts {
    archives packagePlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact packagePlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
