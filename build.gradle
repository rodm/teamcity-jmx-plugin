
plugins {
    id 'org.gradle.java'
    id 'org.gradle.jacoco'
    id 'com.github.rodm.teamcity-server' version '1.1'
    id 'org.sonarqube' version '2.6.2'
}

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "jmx-plugin"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "${archivesBaseName}-${project.version}.zip"

        descriptor {
            name = 'jmxPlugin'
            displayName = 'JMX Plugin'
            version = project.version
            description = 'Makes BuildServer and Agent attributes available via JMX.'
            vendorName = 'Rod MacKenzie'
            vendorUrl = 'https://github.com/rodm'
            email = 'rod.n.mackenzie@gmail.com'
            useSeparateClassloader = false
        }
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
        }
    }
}
