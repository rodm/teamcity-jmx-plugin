
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.4'
    }
}

apply plugin: "com.github.rodm.teamcity"
apply plugin: 'maven-publish'
apply plugin: 'sonar-runner'

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

ext.teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'
ext.teamcityDir = hasProperty('teamcity.home') ? property('teamcity.home') : "$rootDir/servers/TeamCity-${teamcityVersion}"
ext.teamcityDataDir = "$rootDir/data/" + (teamcityVersion =~ /(\d+\.\d+).*/)[0][1]
ext.teamcityJavaHome = System.properties['java.home']

ext.repositoryUrl = hasProperty('repositoryUrl') ? property('repositoryUrl') : 'http://localhost:8080'
ext.repositoryUsername = hasProperty('repositoryUsername') ? property('repositoryUsername') : ''
ext.repositoryPassword = hasProperty('repositoryPassword') ? property('repositoryPassword') : ''

sourceCompatibility = 1.7
targetCompatibility = 1.7

archivesBaseName = "jmx-plugin"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

teamcity {
    version = "${teamcityVersion}"
    descriptor = file("$rootDir/src/main/descriptor/teamcity-plugin.xml")


    homeDir = file(teamcityDir)
    dataDir = file(teamcityDataDir)
    javaHome = file(teamcityJavaHome)
}

artifacts {
    archives packagePlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact packagePlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
