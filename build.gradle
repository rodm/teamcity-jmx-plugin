
plugins {
    id 'org.gradle.java'
    id 'org.gradle.maven-publish'
    id 'org.gradle.sonar-runner'
    id 'com.github.rodm.teamcity-server' version '0.9'
}

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

ext {
    publicationVersion = System.properties['version'] ?: version

    buildVcsNumber = System.properties['build.vcs.number'] ?: new Date().format('yyyyMMddHHmmss')
    buildNumber = System.properties['build.number'] ?: 'dev'

    teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'
    teamcityDir = findProperty('teamcity.dir') ?: "$rootDir/servers"

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

archivesBaseName = "jmx-plugin"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

serverPlugin {
    baseName = project.archivesBaseName
}

teamcity {
    version = "${teamcityVersion}"

    server {
        descriptor = file("$rootDir/src/main/descriptor/teamcity-plugin.xml")
        tokens VERSION: project.version, VCS_REVISION: buildVcsNumber, BUILD_NUMBER: buildNumber

        baseHomeDir = teamcityDir

        environments {
            teamcity81 {
                version = '8.1.5'
                homeDir = file("${baseHomeDir}/TeamCity-${version}")
            }

            teamcity91 {
                version = '9.1.7'
                homeDir = file("${baseHomeDir}/TeamCity-${version}")
            }
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            version publicationVersion

            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
