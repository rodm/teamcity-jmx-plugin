
plugins {
    id 'org.gradle.java'
    id 'org.gradle.jacoco'
    id 'org.gradle.maven-publish'
    id 'com.github.rodm.teamcity-server' version '1.1'
    id 'org.sonarqube' version '2.6.2'
}

group = 'com.github.rodm'
version = '1.1-SNAPSHOT'

ext {
    publicationVersion = System.properties['version'] ?: version

    buildVcsNumber = System.properties['build.vcs.number'] ?: new Date().format('yyyyMMddHHmmss')
    buildNumber = System.properties['build.number'] ?: 'dev'

    teamcityVersion = System.properties['teamcity.version'] ?: '8.1.5'

    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_92'

    repositoryUrl = findProperty('repositoryUrl') ?: 'http://localhost:8080'
    repositoryUsername = findProperty('repositoryUsername') ?: ''
    repositoryPassword = findProperty('repositoryPassword') ?: ''
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "jmx-plugin"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
}

teamcity {
    version = "${teamcityVersion}"

    server {
        archiveName = "${archivesBaseName}-${project.version}.zip"

        descriptor = file("$rootDir/src/main/descriptor/teamcity-plugin.xml")
        tokens VERSION: project.version, VCS_REVISION: buildVcsNumber, BUILD_NUMBER: buildNumber
    }

    environments {
        downloadsDir = project.downloadsDir
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
        }
    }
}

artifacts {
    archives serverPlugin
}

publishing {
    publications {
        plugin(MavenPublication) {
            version publicationVersion

            artifact serverPlugin
        }
    }
    repositories {
        maven {
            url repositoryUrl
            credentials {
                username repositoryUsername
                password repositoryPassword
            }
        }
    }
}
